apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description description
                    url projectUrl
                    scm {
                        url vcsUrl
                    }
                    licenses {
                        license {
                            name 'WTFPL'
                            url 'http://www.wtfpl.net'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'Maksym Lozbin'
                            name 'Maksym Lozbin'
                            email 'maksmtua@gmail.com'
                        }
                        developer {
                            id 'Oleksii Ihnachuk'
                            name 'Oleksii Ihnachuk'
                            email 'legioner.alexei@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = findProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = findProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'nginate'
        licenses = ['WTFPL']
        vcsUrl = vcsUrl
        publish = true
        version {
            name = project.version
            desc = "${project.name} $project.version"
            released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = findProperty('mavenCentralUser') ?: System.getenv('MAVEN_CENTRAL_USER')
                password = findProperty('mavenCentralPass') ?: System.getenv('MAVEN_CENTRAL_PASS')
            }
        }
    }
}

bintrayUpload.dependsOn 'generatePomFileForMavenPublication', 'assemble'